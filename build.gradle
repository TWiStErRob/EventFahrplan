import nerd.tuxmobil.fahrplan.congress.Config
import nerd.tuxmobil.fahrplan.congress.Plugins
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath Plugins.android
        classpath Plugins.androidJunitJacoco
        classpath Plugins.dexcount
        classpath Plugins.kotlin
        classpath Plugins.ksp
        classpath Plugins.sonarQube
        classpath Plugins.unMock
        classpath Plugins.versions
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    tasks.withType(Test).configureEach {
        testLogging {
            events TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }
    tasks.withType(KotlinCompile).configureEach {
        compilerOptions {
            jvmTarget.set(JvmTarget.@Companion.fromTarget(Config.JAVA_VERSION))
            freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn")
        }
    }
}

apply from: "gradle/versions.gradle"
apply plugin: "com.vanniktech.android.junit.jacoco"
